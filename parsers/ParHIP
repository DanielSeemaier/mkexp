# -*-awk-*-
BEGIN {
    header()
    reset()
}

/__BEGIN_FILE__/ {
    sub(/__BEGIN_FILE__ /, "", $0)
    split($0, parts, "___")
    data["Graph"] = parts[1]

    split(parts[2], parts, "_")
    for (i in parts) {
        if (match(parts[i], /k([0-9]+)/, m)) {
            data["K"] = m[1]
        } else if (match(parts[i], /P([0-9]+)x([0-9]+)x([0-9]+)/, m)) {
            data["NumNodes"] = m[1]
            data["NumMPIsPerNode"] = m[2]
            data["NumThreadsPerMPI"] = m[3]
        } else if (match(parts[i], /seed([0-9]+)/, m)) {
            data["Seed"] = m[1]
        } else if (match(parts[i], /eps([0-9\.\-e]+)/, m)) {
            data["Epsilon"] = m[1]
        }
    }
}

/__END_FILE__/ {
    yield()
}

match($0, /n:([0-9]+) m: ([0-9]+)/, m) {
    data["N"] = m[1]
    data["M"] = m[2]
}

match($0, /^Number of vertices:[[:space:]]*([0-9]+)/, m) {
    if (!("N" in data)) {
        data["N"] = m[1]
    }
}
match($0, /^Number of edges:[[:space:]]*([0-9]+)/, m) {
    if (!("M" in data)) {
        data["M"] = m[1]
    }
}

match($0, /log>final edge cut ([0-9]+)/, m) {
    data["Cut"] = m[1]
}

match($0, /log>final balance ([0-9\.e\-]+)/, m) {
    data["Balance"] = m[1] - 1
}

match($0, /log>total partitioning time elapsed ([0-9\.e\-]+)/, m) {
    data["Time"] = m[1]
}

match($0, /timeout/, m) {
    data["Timeout"] = 1
}

#match($0, /RESULT .*time=([0-9\.\-e]+).*/, m) {
    #data["Time"] = m[1]
#}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Seed,"
    printf "Cut,"
    printf "Epsilon,"
    printf "Balance,"
    printf "Time,"
    printf "NumNodes,"
    printf "NumMPIsPerNode,"
    printf "NumThreadsPerMPI,"
    printf "Timeout,"
    printf "Failed\n"
}

function yield() {
    if (length(data) <= 1) { return }

    failed = length(data) < 11

    if (runs == 0) {
        printf "%s,", data["Graph"]
        printf "%d,", data["N"]
        printf "%d,", data["M"]
        printf "%d,", data["K"]
        printf "%d,", data["Seed"]
        printf "%d,", data["Cut"]
        printf "%f,", data["Epsilon"]
        printf "%f,", data["Balance"]
        printf "%f,", data["Time"]
        printf "%d,", data["NumNodes"]
        printf "%d,", data["NumMPIsPerNode"]
        printf "%d,", data["NumThreadsPerMPI"]
        printf "%d,", data["Timeout"]
        printf "%d\n", failed
    }
    
    reset()
}

function reset() {
    split("", data)
    data["Timeout"] = 0
}
