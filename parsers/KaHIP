# -*-awk-*-
BEGIN {
    header()
    reset()
}

/__BEGIN_FILE__/ {
    sub(/__BEGIN_FILE__ /, "", $0)
    split($0, parts, "___")
    data["Graph"] = parts[1]

    split(parts[2], parts, "_")
    for (i in parts) {
        if (match(parts[i], /k([0-9]+)/, m)) {
            data["K"] = m[1]
        } else if (match(parts[i], /P([0-9]+)x([0-9]+)x([0-9]+)/, m)) {
            data["NumNodes"] = m[1]
            data["NumMPIsPerNode"] = m[2]
            data["NumThreadsPerMPI"] = m[3]
        } else if (match(parts[i], /seed([0-9]+)/, m)) {
            data["Seed"] = m[1]
        } else if (match(parts[i], /eps([0-9\.\-e]+)/, m)) {
            data["Epsilon"] = m[1]
        }
    }
}

/__END_FILE__/ {
    yield()
}

# via kaffpa binary
match($0, /graph has ([0-9]+) nodes and ([0-9]+) edges/, m) {
    data["N"] = m[1]
    data["M"] = m[2]
}

match($0, /cut[[:space:]]*([0-9]+)/, m) {
    data["Cut"] = m[1]
}

match($0, /balance[[:space:]]*([0-9\.e\-]+)/, m) {
    data["Balance"] = m[1] - 1
}

match($0, /time spent for partitioning ([0-9\.e\-]+)/, m) {
    data["Time"] = m[1]
}

match($0, /timeout/, m) {
    data["Timeout"] = 1
}

# via KaGen-Partitioner
match($0, /Number of vertices: *([0-9]+)/, m) {
    data["N"] = m[1]
}

match($0, /Number of edges: *([0-9]+)/, m) {
    data["M"] = m[1]
}

match($0, /RESULT.*cut_reported=([0-9]+).*time=([0-9e\.\-]+).*imbalance_computed=([0-9e\.\-]+)/, m) {
    data["Cut"] = m[1]
    data["Time"] = m[2]
    data["Balance"] = m[3]
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Seed,"
    printf "Cut,"
    printf "Epsilon,"
    printf "Imbalance,"
    printf "Time,"
    printf "NumNodes,"
    printf "NumMPIsPerNode,"
    printf "NumThreadsPerMPI,"
    printf "Timeout,"
    printf "Failed\n"
}

function yield() {
    if (length(data) <= 1) { return }

    failed = length(data) < 10

    printf "%s,", data["Graph"]
    printf "%d,", data["N"]
    printf "%d,", data["M"]
    printf "%d,", data["K"]
    printf "%d,", data["Seed"]
    printf "%d,", data["Cut"]
    printf "%f,", data["Epsilon"]
    printf "%f,", data["Balance"]
    printf "%f,", data["Time"]
    printf "1,"
    printf "1," 
    printf "1,"
    printf "%d,", data["Timeout"]
    printf "%d\n", failed
    
    reset()
}

function reset() {
    split("", data)
    data["Timeout"] = 0
}
