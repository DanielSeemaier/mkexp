# -*-awk-*-
BEGIN {
    header()
    reset()
}

/__BEGIN_FILE__/ {
    sub(/__BEGIN_FILE__ /, "", $0)
    split($0, parts, "___")
    data["Graph"] = parts[1]

    split(parts[2], parts, "_")
    for (i in parts) {
        if (match(parts[i], /k([0-9]+)/, m)) {
            data["K"] = m[1]
        } else if (match(parts[i], /P([0-9]+)x([0-9]+)x([0-9]+)/, m)) {
            data["NumNodes"] = m[1]
            data["NumMPIsPerNode"] = m[2]
            data["NumThreadsPerMPI"] = m[3]
        } else if (match(parts[i], /seed([0-9]+)/, m)) {
            data["Seed"] = m[1]
        } else if (match(parts[i], /eps([0-9\.\-e]+)/, m)) {
            data["Epsilon"] = m[1]
        }
    }

    level = 0
}

/__END_FILE__/ {
    yield()
}

match($0, /Number of vertices: *([0-9]+)/, m) {
    data["N"] = m[1]
}

match($0, /Number of edges: *([0-9]+)/, m) {
    data["M"] = m[1]
}

match($0, /MaxRSS before partitioning: ([0-9]+)/, m) {
    data["MemoryBeforePartitioning"] = m[1]
}

match($0, /MaxRSS after partitioning: ([0-9]+)/, m) {
    data["MemoryAfterPartitioning"] = m[1]
}

match($0, /RESULT .*nthreads=([0-9]+) cut_reported=([0-9]+) cut_computed=([0-9]+) time=([0-9\.\-e]+) imbalance_computed=([0-9\.\-e]+)/, m) {
    data["NumThreadsPerMPI"] = m[1]
    data["ReportedCut"] = m[2]
    data["Cut"] = m[3]
    data["Time"] = m[4]
    data["Balance"] = m[5]
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Seed,"
    printf "ReportedCut,"
    printf "Cut,"
    printf "Epsilon,"
    printf "Imbalance,"
    printf "Time,"
    printf "NumNodes,"
    printf "NumMPIsPerNode,"
    printf "NumThreadsPerMPI,"
    printf "MaxRSSAfterIO,"
    printf "MaxRSSAfterPartitioning,"
    printf "Failed\n"
}

function yield() {
    if (length(data) == 0) { 
        return 
    }
    failed = length(data) < 12

    printf "%s,", data["Graph"]
    printf "%d,", data["N"]
    printf "%d,", data["M"]
    printf "%d,", data["K"]
    printf "%d,", data["Seed"]
    printf "%d,", data["ReportedCut"]
    printf "%d,", data["Cut"]
    printf "%f,", data["Epsilon"]
    printf "%f,", data["Balance"]
    printf "%f,", data["Time"]
    printf "%d,", data["NumNodes"]
    printf "%d,", data["NumMPIsPerNode"]
    printf "%d,", data["NumThreadsPerMPI"]
    printf "%d,", data["MemoryBeforePartitioning"]
    printf "%d,", data["MemoryAfterPartitioning"]
    printf "%d\n", failed
    
    reset()
}

function reset() {
    split("", data)
    Summary = 0
}
